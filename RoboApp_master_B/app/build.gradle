apply plugin: 'com.android.application'

android {
    compileSdkVersion 19
    buildToolsVersion "21.1.1"

    //disable default ndk Android.mk
    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/jniLibs'
    }


    defaultConfig {
        applicationId "com.robocat.roboappui"
        minSdkVersion 14
        targetSdkVersion 19

        ndk{
            moduleName "detection_based_tracker"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:19.1.0'
    compile files('libs/usb-serial-for-android-v010.jar')
    compile project(':openCVLibrary2410')
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(ndkBuild)
}

//Using custom Android.mk with Gradle/Android Studio
//@see http://blog.gaku.net/ndk/
task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {
    def ndkDir =  plugins.getPlugin('com.android.application').sdkHandler.getNdkFolder()
    commandLine "$ndkDir/ndk-build.cmd",
            'NDK_PROJECT_PATH=build',
            'NDK_LIBS_OUT=src/main/jniLibs',
            'APP_BUILD_SCRIPT=src/main/jni/Android.mk',
            'NDK_APPLICATION_MK=src/main/jni/Application.mk'
}

